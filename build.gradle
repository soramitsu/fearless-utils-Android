apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

buildscript {
    ext {
        // App version
        versionName = '1.0.121'
        versionCode = 1

        // SDK and tools
        compileVersion = 33
        minVersion = 24
        targetVersion = 33
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.0'
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.20"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.8.20"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.4.1.3373"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
        mavenCentral()
    }
}

configurations {
    ktlint
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    ktlint "com.pinterest:ktlint:0.40.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "$project.rootDir/**/src/main/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/checkstyle/kotlin-checkstyle-report.xml"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "$project.rootDir/**/src/main/**/*.kt"
}


tasks.named('testReleaseUnitTest') {
    useJUnitPlatform()
    finalizedBy testCoverage
}

sonarqube {
    properties {
        property "sonar.projectKey", "fearless:fearless-utils-Android"
        property "sonar.projectName", "fearless-utils-Android"
        property "sonar.exclusions", "${project.projectDir}/**/*.txt"
    }
}

jacoco {
    toolVersion = "0.8.8"
    reportsDir = file("$buildDir/reports/")
}

task testCoverage(type: JacocoReport) {
    executionData tasks.withType(Test)
    classDirectories.from = files(sourceSets.main.output.classesDirs)
    sourceDirectories.from = files(sourceSets.main.java.srcDirs)

    reports {
        xml.enabled = true
    }
}