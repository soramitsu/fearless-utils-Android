buildscript {
    ext {
        // App version
        versionName = '1.0.96'
        versionCode = 1

        // SDK and tools
        compileVersion = 28
        minVersion = 21
        targetVersion = 28

        kotlinVersion = '1.4.31'

        supportVersion = '1.0.0'
        constraintVersion = '1.1.3'

        rxJavaVersion = '2.2.7'
        coroutinesVersion = '1.4.2'

        gsonVersion = '2.8.2'

        zXingVersion = '3.5.0'

        spoungyCastleVersion = '1.58.0.0'
        ed25519Version = '0.3.0'
        bip39version = '2019.01.27'
        web3jVersion = '4.6.0-android'
        apacheXercesVersion = '2.9.0'

        junitVersion = '4.12'
        mockitoVersion = '2.23.0'
        wsVersion = '2.10'
        xxHashVersion = '1.7.1'
        svgVersion = '1.4'

        androidDep = "androidx.appcompat:appcompat:$supportVersion"
        constraintDep = "androidx.constraintlayout:constraintlayout:$constraintVersion"
        materialDep = "com.google.android.material:material:$supportVersion"

        rxJavaDep = "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        coroutinesDep = "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

        gsonDep = "com.google.code.gson:gson:$gsonVersion"

        spoungyCastleBcPixDep = "com.madgag.spongycastle:bcpkix-jdk15on:$spoungyCastleVersion"
        spoungyCastleBcPgDep = "com.madgag.spongycastle:bcpg-jdk15on:$spoungyCastleVersion"
        ed25519Dep = "net.i2p.crypto:eddsa:$ed25519Version"
        web3jDep = "org.web3j:crypto:$web3jVersion"
        apacheXercesDep = "org.eclipse.birt.runtime.3_7_1:org.apache.xerces:$apacheXercesVersion"

        bip39Dep = "io.github.novacrypto:BIP39:$bip39version"
        secureStringDep = "io.github.novacrypto:securestring:$bip39version@jar"
        wsDep = "com.neovisionaries:nv-websocket-client:$wsVersion"
        xxHashDep = "org.lz4:lz4-java:$xxHashVersion"
        svgDep = "com.caverock:androidsvg-aar:$svgVersion"

        jUnitDep = "junit:junit:$junitVersion"
        mockitoDep = "org.mockito:mockito-inline:$mockitoVersion"

        androidTestRunnerDep = 'androidx.test:runner:1.4.0'
        androidTestRulesDep = 'androidx.test:rules:1.4.0'
        androidJunitDep = 'androidx.test.ext:junit:1.1.3'
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0'
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
        mavenCentral()
    }
}

configurations {
    ktlint
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    ktlint "com.pinterest:ktlint:0.40.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "$project.rootDir/**/src/main/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/checkstyle/kotlin-checkstyle-report.xml"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "$project.rootDir/**/src/main/**/*.kt"
}
